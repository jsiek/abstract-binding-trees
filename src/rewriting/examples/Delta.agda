{-# OPTIONS --without-K --rewriting #-}
{-
  This is a language without lexical scoping, but otherwise similar to the lambda calculus.
-}

open import Data.List using (List; []; _âˆ·_; length)
open import Data.Nat using (â„•; zero; suc)
open import Data.Product using (_Ã—_; _,_; projâ‚; projâ‚‚; âˆƒ-syntax)
open import Data.Unit.Polymorphic using (âŠ¤; tt)
open import Data.Vec using (Vec) renaming ([] to []ÌŒ; _âˆ·_ to _âˆ·ÌŒ_)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; sym)
open import Sig

module rewriting.examples.Delta where

data Op : Set where
  op-delta : Op
  op-app : Op
  op-lit : â„• â†’ Op
  op-cons : Op
  op-fst : Op
  op-snd : Op

sig : Op â†’ List Sig
sig op-delta = Î½ â–  âˆ· []
sig op-app = â–  âˆ· â–  âˆ· []
sig (op-lit k) = []
sig op-cons = â–  âˆ· â–  âˆ· []
sig op-fst = â–  âˆ· []
sig op-snd = â–  âˆ· []

open import rewriting.AbstractBindingTree Op sig

pattern $ k  = op-lit k â¦… nil â¦†

pattern Î´ N  = op-delta â¦… cons (bind (ast N)) nil â¦†

infixl 7  _Â·_
pattern _Â·_ L M = op-app â¦… cons (ast L) (cons (ast M) nil) â¦†

infixl 7  âŸ¨_,_âŸ©
pattern âŸ¨_,_âŸ© L M = op-cons â¦… cons (ast L) (cons (ast M) nil) â¦†

pattern fst L = op-fst â¦… (cons (ast L) nil) â¦†

pattern snd L = op-snd â¦… (cons (ast L) nil) â¦†

Term : Set
Term = ABT

{-------------      Examples regarding substitution   -------------}

sub-app : âˆ€ (L M : Term) (Ïƒ : Subst) â†’ âŸª Ïƒ âŸ« (L Â· M) â‰¡ (âŸª Ïƒ âŸ« L) Â· (âŸª Ïƒ âŸ« M)
sub-app = Î» L M Ïƒ â†’ refl

sub-delta : âˆ€ (N : Term) (Ïƒ : Subst) â†’ âŸª Ïƒ âŸ« (Î´ N) â‰¡ Î´ (âŸª ` 0 â€¢ (Ïƒ â¨Ÿ â†‘) âŸ« N)
sub-delta N Ïƒ = refl

_ : âˆ€ (M L : Term) â†’ (M â€¢ L â€¢ id) 0 â‰¡ M
_ = Î» M L â†’ refl

_ : âˆ€ (M L : Term) â†’ (M â€¢ L â€¢ id) 1 â‰¡ L
_ = Î» M L â†’ refl

_ : âˆ€ (M L : Term) â†’ (M â€¢ L â€¢ id) 2 â‰¡ ` 0
_ = Î» M L â†’ refl

_ : âˆ€ (M L : Term) â†’ (M â€¢ L â€¢ id) 3 â‰¡ ` 1
_ = Î» M L â†’ refl

{-# REWRITE sub-var #-}

_ : âˆ€ (M L : Term) â†’ âŸª M â€¢ L â€¢ id âŸ« (` 1 Â· ` 0) â‰¡ L Â· M
_ = Î» M L â†’ refl

_ : âˆ€ (M : Term) â†’ âŸª M â€¢ id âŸ« (` 1 Â· ` 0) â‰¡ ` 0 Â· M
_ = Î» M â†’ refl

_ : âˆ€ (N L : Term) â†’ ((` 1 Â· ` 0) [ N ] ) [ L ] â‰¡ (L Â· N [ L ])
_ = Î» N L â†’ refl

{-------------      Reduction Semantics    -------------}

data Value : Term â†’ Set where

  V-$ : âˆ€ {k : â„•}
      ---------------------------
    â†’ Value ($ k)

  V-Î´ : âˆ€ {N : Term}
      ---------------------------
    â†’ Value (Î´ N)

  V-cons : âˆ€ {M N : Term}
    â†’ Value M
    â†’ Value N
      ---------------------------
    â†’ Value âŸ¨ M , N âŸ©


infix 2 _â€”â†’_

data _â€”â†’_ : Term â†’ Term â†’ Set where

  Î¾-Â·â‚ : âˆ€ {L Lâ€² M : Term}
    â†’ L â€”â†’ Lâ€²
      ---------------
    â†’ L Â· M â€”â†’ Lâ€² Â· M

  Î¾-Â·â‚‚ : âˆ€ {V M Mâ€² : Term}
    â†’ Value V
    â†’ M â€”â†’ Mâ€²
      ---------------
    â†’ V Â· M â€”â†’ V Â· Mâ€²

  Î²-Î´ : âˆ€ {N W : Term}
    â†’ Value W
      --------------------
    â†’ (Î´ N) Â· W â€”â†’ N [ W ]

  Î¾-consâ‚ : âˆ€ {L Lâ€² M : Term}
    â†’ L â€”â†’ Lâ€²
      ---------------
    â†’ âŸ¨ L , M âŸ© â€”â†’ âŸ¨ Lâ€² , M âŸ©

  Î¾-consâ‚‚ : âˆ€ {V M Mâ€² : Term}
    â†’ Value V
    â†’ M â€”â†’ Mâ€²
      ---------------
    â†’ âŸ¨ V , M âŸ© â€”â†’ âŸ¨ V , Mâ€² âŸ©
    
  Î¾-fst : âˆ€ {L Lâ€² : Term}
    â†’ L â€”â†’ Lâ€²
      ---------------
    â†’ fst L â€”â†’ fst Lâ€²

  Î¾-snd : âˆ€ {L Lâ€² : Term}
    â†’ L â€”â†’ Lâ€²
      ---------------
    â†’ snd L â€”â†’ snd Lâ€²

  Î²-fst : âˆ€ {V W : Term}
    â†’ Value V
    â†’ Value W
      --------------------
    â†’ fst âŸ¨ V , W âŸ© â€”â†’ V

  Î²-snd : âˆ€ {V W : Term}
    â†’ Value V
    â†’ Value W
      --------------------
    â†’ snd âŸ¨ V , W âŸ© â€”â†’ W

{-------------      Type System    -------------}


data Type : Set where
  Nat   : Type
  _â‡’_   : Type â†’ Type â†’ Type
  _`Ã—_  : Type â†’ Type â†’ Type

open import Var

ğ‘ƒ : (op : Op) â†’ Vec Type (length (sig op)) â†’ BTypes Type (sig op) â†’ Type â†’ Set
ğ‘ƒ op-delta (B âˆ·ÌŒ []ÌŒ) ( ( A , tt ) , tt ) Aâ†’B = Aâ†’B â‰¡ A â‡’ B
ğ‘ƒ op-app (Aâ†’B âˆ·ÌŒ A âˆ·ÌŒ []ÌŒ) ( tt , ( tt , tt )) B = Aâ†’B â‰¡ A â‡’ B
ğ‘ƒ (op-lit k) []ÌŒ tt A = A â‰¡ Nat
ğ‘ƒ op-cons (A âˆ·ÌŒ B âˆ·ÌŒ []ÌŒ) ( tt , ( tt , tt )) C = C â‰¡ A `Ã— B
ğ‘ƒ op-fst (AÃ—B âˆ·ÌŒ []ÌŒ) ( tt , tt) A = âˆƒ[ B ] (AÃ—B â‰¡ A `Ã— B)
ğ‘ƒ op-snd (AÃ—B âˆ·ÌŒ []ÌŒ) ( tt , tt) B = âˆƒ[ A ] (AÃ—B â‰¡ A `Ã— B)

open import rewriting.ABTPredicate Op sig ğ‘ƒ

{-------------      Type System Rules    -------------}

pattern âŠ¢` âˆ‹x = var-p âˆ‹x
pattern âŠ¢$ k eq = op-p {op = (op-lit k)} nil-p eq
pattern âŠ¢Î´ âŠ¢N eq = op-p {op = op-delta} (cons-p (bind-p (ast-p âŠ¢N)) nil-p) eq
pattern âŠ¢Â· âŠ¢L âŠ¢M eq = op-p {op = op-app}
                           (cons-p (ast-p âŠ¢L) (cons-p (ast-p âŠ¢M) nil-p)) eq
pattern âŠ¢cons âŠ¢L âŠ¢M eq = op-p {op = op-cons}
                           (cons-p (ast-p âŠ¢L) (cons-p (ast-p âŠ¢M) nil-p)) eq
pattern âŠ¢fst âŠ¢L eq = op-p {op = op-fst} (cons-p (ast-p âŠ¢L) nil-p) eq
pattern âŠ¢snd âŠ¢L eq = op-p {op = op-snd} (cons-p (ast-p âŠ¢L) nil-p) eq

{-------------      Proof of Progress    -------------}

data Progress (M : Term) : Set where

  step : âˆ€ {N}
    â†’ M â€”â†’ N
      ----------
    â†’ Progress M

  done :
      Value M
      ----------
    â†’ Progress M

progress : âˆ€ {M A}
  â†’ [] âŠ¢ M â¦‚ A
    ----------
  â†’ Progress M
  
progress (âŠ¢` ())

progress (âŠ¢$ k _)                           =  done V-$

progress (âŠ¢Î´ âŠ¢N _)                          =  done V-Î´

progress (âŠ¢Â· âŠ¢L âŠ¢M eq)
    with progress âŠ¢L
... | step Lâ€”â†’Lâ€²                            =  step (Î¾-Â·â‚ Lâ€”â†’Lâ€²)
... | done V-Î´
    with progress âŠ¢M
... | step Mâ€”â†’Mâ€²                            =  step (Î¾-Â·â‚‚ V-Î´ Mâ€”â†’Mâ€²)
... | done v                                =  step (Î²-Î´ v)
progress (âŠ¢Â· âŠ¢L âŠ¢M eq)
    | done (V-cons v w)
    with âŠ¢L | eq
... | âŠ¢cons âŠ¢V âŠ¢W refl | ()
progress (âŠ¢Â· âŠ¢L âŠ¢M eq)
    | done V-$
    with âŠ¢L | eq
... | âŠ¢$ k refl | ()

progress (âŠ¢cons âŠ¢L âŠ¢M eq)
    with progress âŠ¢L
... | step Lâ€”â†’Lâ€²                            =  step (Î¾-consâ‚ Lâ€”â†’Lâ€²)
... | done v
    with progress âŠ¢M
... | step Mâ€”â†’Mâ€²                            =  step (Î¾-consâ‚‚ v Mâ€”â†’Mâ€²)
... | done w                                =  done (V-cons v w)

progress (âŠ¢fst âŠ¢L eq)
    with progress âŠ¢L
... | step Lâ€”â†’Lâ€²                            =  step (Î¾-fst Lâ€”â†’Lâ€²)
... | done (V-cons v w)                     =  step (Î²-fst v w)
progress (âŠ¢fst âŠ¢L eq)
    | done V-Î´
    with âŠ¢L | eq
... | âŠ¢Î´ âŠ¢N refl | ()
progress (âŠ¢fst âŠ¢L eq)
    | done V-$
    with âŠ¢L | eq
... | âŠ¢$ k refl | ()

progress (âŠ¢snd âŠ¢L eq)
    with progress âŠ¢L
... | step Lâ€”â†’Lâ€²                            =  step (Î¾-snd Lâ€”â†’Lâ€²)
... | done (V-cons v w)                     =  step (Î²-snd v w)
progress (âŠ¢snd âŠ¢L eq)
    | done V-Î´
    with âŠ¢L | eq
... | âŠ¢Î´ âŠ¢N refl | ()
progress (âŠ¢snd âŠ¢L eq)
    | done V-$
    with âŠ¢L | eq
... | âŠ¢$ k refl | ()


{-------------      Proof of Preservation    -------------}

preserve : âˆ€ {Î“ M N A}
  â†’ Î“ âŠ¢ M â¦‚ A
  â†’ M â€”â†’ N
    ----------
  â†’ Î“ âŠ¢ N â¦‚ A
preserve (âŠ¢Â· âŠ¢L âŠ¢M refl) (Î¾-Â·â‚ Lâ€”â†’Lâ€²) = âŠ¢Â· (preserve âŠ¢L Lâ€”â†’Lâ€²) âŠ¢M refl
preserve (âŠ¢Â· âŠ¢L âŠ¢M refl) (Î¾-Â·â‚‚ v Mâ€”â†’Mâ€²) = âŠ¢Â· âŠ¢L (preserve âŠ¢M Mâ€”â†’Mâ€²) refl
preserve {Î“}{(Î´ N) Â· M}{_}{B} (âŠ¢Â· (âŠ¢Î´ âŠ¢N refl) âŠ¢M refl) (Î²-Î´ {N = N} v) =
    preserve-substitution N M âŠ¢N âŠ¢M
preserve {Î“} {.(âŸ¨ _ , _ âŸ©)} {_} {B} (âŠ¢cons âŠ¢M âŠ¢N refl) (Î¾-consâ‚ red) =
    âŠ¢cons (preserve âŠ¢M red) âŠ¢N refl
preserve {Î“} {.(âŸ¨ _ , _ âŸ©)} {_} {B} (âŠ¢cons âŠ¢M âŠ¢N refl) (Î¾-consâ‚‚ v red) =
   âŠ¢cons âŠ¢M (preserve âŠ¢N red) refl
preserve {Î“} {.(fst _)} {_} {A} (âŠ¢fst âŠ¢L (B , refl)) (Î¾-fst red) =
    âŠ¢fst (preserve âŠ¢L red) (B , refl)
preserve {Î“} {.(snd _)} {_} {B} (âŠ¢snd âŠ¢L (A , refl)) (Î¾-snd red) =
    âŠ¢snd (preserve âŠ¢L red) (A , refl)
preserve {Î“} {.(fst âŸ¨ _ , _ âŸ©)} {_} {B} (âŠ¢fst (âŠ¢cons âŠ¢V âŠ¢W refl) (_ , refl)) (Î²-fst v w) = âŠ¢V
preserve {Î“} {.(snd âŸ¨ _ , _ âŸ©)} {_} {B} (âŠ¢snd (âŠ¢cons âŠ¢V âŠ¢W refl) (_ , refl)) (Î²-snd v w) = âŠ¢W

{- TODO: Add confluence proof to show off the substitution lemma. -}

{-------------      Denotational Semantics    -------------}

